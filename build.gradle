plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
}

group = 'io.infracheck'
version = '1.0.1'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenLocal()  // core 모듈 로컬 참조용
    mavenCentral()
}

dependencies {
    // Gradle API (java-gradle-plugin이 자동 추가하지만 IDE 인식을 위해 명시)
    compileOnly gradleApi()

    // core 모듈 (공통 분석 로직)
    implementation 'io.infracheck:infrastructure-analyzer-core:1.0.1'

    // JSON 직렬화 (Task에서 직접 사용)
    implementation 'com.google.code.gson:gson:2.10.1'
}

gradlePlugin {
    plugins {
        infrastructureAnalyzer {
            id = 'io.infracheck.infrastructure-analyzer'
            implementationClass = 'io.infracheck.gradle.InfrastructureAnalyzerPlugin'
        }
    }
}

publishing {
    repositories {
        mavenLocal()  // 로컬 테스트용
        maven {
            url = uri(findProperty('nexusUrl') ?: "https://nexus.company.com/repository/maven-releases/")
            credentials {
                username = findProperty('nexusUsername') ?: System.getenv("NEXUS_USERNAME")
                password = findProperty('nexusPassword') ?: System.getenv("NEXUS_PASSWORD")
            }
        }
    }
}
